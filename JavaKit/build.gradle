//===----------------------------------------------------------------------===//
 //
 // This source file is part of the Swift.org open source project
 //
 // Copyright (c) 2024 Apple Inc. and the Swift.org project authors
 // Licensed under Apache License v2.0
 //
 // See LICENSE.txt for license information
 // See CONTRIBUTORS.txt for the list of Swift.org project authors
 //
 // SPDX-License-Identifier: Apache-2.0
 //
 //===----------------------------------------------------------------------===//

import groovy.json.JsonSlurper

plugins {
  id("build-logic.java-library-conventions")
}

group = "org.swift.javakit"
version = "1.0-SNAPSHOT"

repositories {
  mavenCentral()
}

java {
  toolchain {
    languageVersion.set(JavaLanguageVersion.of(22))
  }
}

List<String> loadDependenciesFromSwiftJavaJSON() {
  def jsonFile = file("../Sources/JavaKitDependencyResolver/swift-java.config")
  if (!jsonFile.exists()) {
    throw new FileNotFoundException("Dependencies must be declared in Swift config file! Not found: ${jsonFile.absolutePath}")
  }
  def jsonContent = new JsonSlurper().parseText(jsonFile.text)
  return jsonContent.dependencies
}

dependencies {
  // We load dependencies from one source of truth: the swift-java.config of module
  // that is used to access this Java code.
  loadDependenciesFromSwiftJavaJSON().each {
    implementation(it)
  }

  testImplementation(platform("org.junit:junit-bom:5.10.0"))
  testImplementation("org.junit.jupiter:junit-jupiter")
}

tasks.test {
  useJUnitPlatform()
  testLogging {
    events("passed", "skipped", "failed")
  }
}
