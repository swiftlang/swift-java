//===----------------------------------------------------------------------===//
//
// This source file is part of the Swift.org open source project
//
// Copyright (c) 2024 Apple Inc. and the Swift.org project authors
// Licensed under Apache License v2.0
//
// See LICENSE.txt for license information
// See CONTRIBUTORS.txt for the list of Swift.org project authors
//
// SPDX-License-Identifier: Apache-2.0
//
//===----------------------------------------------------------------------===//

import org.swift.swiftkit.gradle.BuildUtils

plugins {
    id("build-logic.java-application-conventions")
    id("me.champeau.jmh") version "0.7.2"
}

group = "org.swift.swiftkit"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(22))
    }
}


def jextract = tasks.register("jextract", Exec) {
    description = "Extracts Java accessor sources using jextract"

    outputs.dir(layout.buildDirectory.dir("generated/sources/jextract/main"))
    inputs.dir("$rootDir/Sources/ExampleSwiftLibrary") // monitored library

    // any changes in the source generator sources also mean the resulting output might change
    inputs.dir("$rootDir/Sources/JExtractSwift")
    inputs.dir("$rootDir/Sources/JExtractSwiftTool")

    workingDir = rootDir
    commandLine "make"
    args "jextract-generate"
}

sourceSets {
    main {
        java {
            srcDir(jextract)
        }
    }
}

dependencies {
    implementation(project(':SwiftKit'))

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

application {
    mainClass = "com.example.swift.HelloJava2Swift"

    // In order to silence:
    //   WARNING: A restricted method in java.lang.foreign.SymbolLookup has been called
    //   WARNING: java.lang.foreign.SymbolLookup::libraryLookup has been called by org.example.swift.JavaKitExample in an unnamed module
    //   WARNING: Use --enable-native-access=ALL-UNNAMED to avoid a warning for callers in this module
    //   WARNING: Restricted methods will be blocked in a future release unless native access is enabled
    // FIXME: Find out the proper solution to this
    applicationDefaultJvmArgs = [
        "--enable-native-access=ALL-UNNAMED",

        // Include the library paths where our dylibs are that we want to load and call
        "-Djava.library.path=" + BuildUtils.javaLibraryPaths(rootDir).join(":"),

        // Enable tracing downcalls (to Swift)
        "-Djextract.trace.downcalls=true"
    ]
}

jmh {
    jvmArgsAppend = [
        "-Djava.library.path=" + BuildUtils.javaLibraryPaths(rootDir).join(":"),
    ]
}
