//===----------------------------------------------------------------------===//
//
// This source file is part of the Swift.org open source project
//
// Copyright (c) 2024 Apple Inc. and the Swift.org project authors
// Licensed under Apache License v2.0
//
// See LICENSE.txt for license information
// See CONTRIBUTORS.txt for the list of Swift.org project authors
//
// SPDX-License-Identifier: Apache-2.0
//
//===----------------------------------------------------------------------===//

import org.swift.swiftkit.gradle.BuildUtils

import java.nio.file.Files
import java.nio.file.Paths

plugins {
    id("build-logic.java-application-conventions")
//     id("me.champeau.jmh") version "0.7.2"
}

group = "org.swift.swiftkit"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(22))
    }
}


// This is for development, when we edit the Swift swift-java project, the outputs of the generated sources may change.
// Thus, we also need to watch and re-build the top level project.
def buildJExtractPlugin = tasks.register("swiftBuildJExtractPlugin", Exec) {
    description = "Rebuild the swift-java root project"

    inputs.file(new File(rootDir, "Package.swift"))
    inputs.dir(new File(rootDir, "Sources"))
    outputs.dir(new File(rootDir, ".build"))

    workingDir = rootDir
    commandLine "swift"
    args "build"
}

def buildSwift = tasks.register("swiftBuildProject", Exec) {
    description = "Builds swift sources, including swift-java source generation"
    dependsOn buildJExtractPlugin

    // only because we depend on "live developing" the plugin while using this project to test it
    inputs.file(new File(rootDir, "Package.swift"))
    inputs.dir(new File(rootDir, "Sources"))

    // TODO: we can use package describe --type json to figure out which targets depend on JExtractSwiftPlugin and will produce outputs
    inputs.file(layout.projectDirectory.file("Package.swift"))
    inputs.dir(layout.projectDirectory.dir("Sources"))

//    outputs.dir(layout.buildDirectory.dir("../.build/plugins/outputs/${layout.projectDirectory.asFile.getName().toLowerCase()}/JExtractPluginSampleLib/destination/JExtractSwiftPlugin/src/generated/java"))
    outputs.dir(layout.buildDirectory.dir("../.build/plugins/outputs/${layout.projectDirectory.asFile.getName().toLowerCase()}"))

    File baseSwiftPluginOutputsDir = layout.buildDirectory.dir("../.build/plugins/outputs/").get().asFile
    if (!baseSwiftPluginOutputsDir.exists()) {
        baseSwiftPluginOutputsDir.mkdirs()
    }
    Files.walk(layout.buildDirectory.dir("../.build/plugins/outputs/").get().asFile.toPath()).each {
        if (it.endsWith("JExtractSwiftPlugin/src/generated/java")) {
            outputs.dir(it)
            println("OUTPUT = ${it}")
        }
    }

    workingDir = layout.projectDirectory
    commandLine "swift"
    args "build"
}

// Add the java-swift generated Java sources
sourceSets {
    main {
        java {
            srcDir(buildSwift)
        }
    }
}

dependencies {
    implementation(project(':SwiftKit'))

    testImplementation(platform("org.junit:junit-bom:5.10.0"))
    testImplementation("org.junit.jupiter:junit-jupiter")

    // implementation(swiftDynamicLibrary('')
}

tasks.named('test', Test) {
    useJUnitPlatform()
}

application {
    mainClass = "com.example.swift.JExtractPluginSampleMain"

    // In order to silence:
    //   WARNING: A restricted method in java.lang.foreign.SymbolLookup has been called
    //   WARNING: java.lang.foreign.SymbolLookup::libraryLookup has been called by org.example.swift.JavaKitExample in an unnamed module
    //   WARNING: Use --enable-native-access=ALL-UNNAMED to avoid a warning for callers in this module
    //   WARNING: Restricted methods will be blocked in a future release unless native access is enabled
    // FIXME: Find out the proper solution to this
    applicationDefaultJvmArgs = [
        "--enable-native-access=ALL-UNNAMED",

        // Include the library paths where our dylibs are that we want to load and call
        "-Djava.library.path=" +
                (BuildUtils.javaLibraryPaths(rootDir) +
                 BuildUtils.javaLibraryPaths(layout.projectDirectory.asFile)).join(":"),

        // Enable tracing downcalls (to Swift)
        "-Djextract.trace.downcalls=true"
    ]
}
